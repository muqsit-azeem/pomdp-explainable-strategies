pomdp

observables
start
endobservables

const int N;
const int ENERGY;
const int RESOLUTION = 2;
const int fuelCAP = ENERGY;
const int axMAX = N-1;
const int ayMAX = N-1;
const int axMIN = 0;
const int ayMIN = 0;
const int ob1x = axMAX-1;
const int ob1y = ayMAX-1;
const int rf1x = axMIN;
const int rf1y = ayMIN;
const int rf2x = floor(N/2)-1;
const int rf2y = floor(N/2)-1;

formula westenabled = ax != axMIN;
formula eastenabled = ax != axMAX;
formula northenabled = ay != ayMIN;
formula southenabled = ay != ayMAX;
observable "cangowest" = westenabled;
observable "cangoeast" = eastenabled;
observable "cangonorth" = northenabled;
observable "cangosouth" = southenabled;
formula done = start & ax = axMAX & ay = ayMAX;
observable "amdone" = done;
formula crash =  (ax = ob1x & ay = ob1y);
observable "hascrash" = crash;
formula atStation = (ax = rf1x & ay = rf1y) | (ax = rf2x & ay = rf2y);
formula canRefuel = atStation  & fuel < fuelCAP;
observable "refuelAllowed" = canRefuel;
observable "fuelNONempty" = fuel > 0;
observable "fuelfull" = fuel = ENERGY;
observable "fuelmeter" = floor(fuel/(ENERGY/RESOLUTION));

module master
    start : bool init false;

    [placement] !start -> (start'=true);
    [north] start & !done -> true;
    [south] start  & !done -> true;
    [east] start  & !done-> true;
    [west] start & !done -> true;
    [done] start & done -> true;
endmodule


module tank
    fuel : [0..fuelCAP] init fuelCAP;

    [refuel] canRefuel -> 1:(fuel'=fuelCAP);
    [north] fuel > 0 & !canRefuel -> 1:(fuel'=fuel-1);
    [south] fuel > 0 & !canRefuel -> 1:(fuel'=fuel-1);
    [east] fuel > 0 & !canRefuel -> 1:(fuel'=fuel-1);
    [west] fuel > 0 & !canRefuel -> 1:(fuel'=fuel-1);
    [empty] fuel = 0 & !canRefuel -> 1:(fuel'=0);
endmodule

module rover
    ax : [axMIN..axMAX] init 0;
    ay : [ayMIN..ayMAX] init 0;

    [placement] true ->  1: (ax'=0) & (ay'=0);

    [west] westenabled -> 1: (ax'=max(ax-1,axMIN));
    [east] eastenabled -> 1:(ax'=min(ax+1,axMAX));
    [south]  southenabled -> 1: (ay'=min(ay+1,ayMAX));
    [north]  northenabled -> 1: (ay'=max(ay-1,ayMIN));
endmodule

label "goal" = done;
label "notbad" =  !crash & (fuel > 0 | canRefuel);
