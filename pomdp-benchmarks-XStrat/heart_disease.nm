pomdp

observables
start, turn, done, ecgResult, stressTestResult, chestPain, mi
endobservables

formula bad =  (mi = 1);
formula good = (cad=0) & (ischemia=0) & start;
formula isDone = done;

global cad : [0..2] init 0; // Coronary artery disease; 0: absent, 1: mild, 2: severe
global ischemia : [0..2] init 0; // 0: absent, 1: mild, 2: severe
global done : bool init false;


module master
    start : bool init false;
    turn : bool init false;

    [start] !start -> (start'=true);
    [ecg] (start=true) & (turn=false) & !done -> (turn'=true);
    [ST] (start=true) & (turn=false) & !done -> (turn'=true);
    [medicine] (start=true) & (turn=false) & !done -> (turn'=true);
    [surgery] (start=true) & (turn=false) & !done -> (turn'=true);
    // [wait] (start=true) & (turn=false) & !done -> (turn'=true);
    [finish] (start=true) & (turn=false) & !done -> (turn'=true);
    [MI] (start=true) & (turn=true) & !done -> (turn'=false);
    [done] done -> (done'=true);
 endmodule


module doctor
    // done : bool init false;
    ecgResult : [0..2] init 0; // 0: report not ready, 1: normal, 2: abnormal
    stressTestResult : [0..2] init 0; // 0: report not ready, 1: normal, 2: abnormal

    // investigate
    
    [ecg] (cad=0) & (ischemia=0) -> 1 : (ecgResult'=1);
    [ecg] (cad=1) & (ischemia=0) -> 0.4 : (ecgResult'=1) + 0.6 : (ecgResult'=2);
    [ecg] (cad=0) & (ischemia=1) -> 0.4 : (ecgResult'=1) + 0.6 : (ecgResult'=2);
    [ecg] (cad=1) & (ischemia=1) -> 0.1 : (ecgResult'=1) + 0.9 : (ecgResult'=2);

    [ST] (cad=0) & (ischemia=0) -> 1 : (stressTestResult'=1);
    [ST] (cad=1) & (ischemia=0) -> 0.5 : (stressTestResult'=1) + 0.5 : (stressTestResult'=2);
    [ST] (cad=0) & (ischemia=1) -> 0.5 : (stressTestResult'=1) + 0.5 : (stressTestResult'=2);
    [ST] (cad=1) & (ischemia=1) -> 0.25 : (stressTestResult'=1) + 0.75 : (stressTestResult'=2);

    // treatment
    // [wait] true -> true;

    [medicine] (cad=0) & (ischemia=0) -> 1 : (cad'=0) & (ischemia'=0);
    [medicine] (cad=1) & (ischemia=0) -> 0.3 : (cad'=0) & (ischemia'=0) +
                                        0.7 : (cad'=1) & (ischemia'=0);
    [medicine] (cad=0) & (ischemia=1) -> 0.2 : (cad'=0) & (ischemia'=0) +
                                        0.8 : (cad'=0) & (ischemia'=1);
    [medicine] (cad=1) & (ischemia=1) -> 0.06 : (cad'=0) & (ischemia'=0) +
                                        0.24 : (cad'=0) & (ischemia'=1) +
                                        0.14 : (cad'=1) & (ischemia'=0) +
                                        0.56 : (cad'=1) & (ischemia'=1);

    [surgery] (cad=0) & (ischemia=0) -> 1 : (cad'=0) & (ischemia'=0);
    [surgery] (cad=1) & (ischemia=0) -> 0.9 : (cad'=0) & (ischemia'=0) +
                                        0.1 : (cad'=1) & (ischemia'=0);
    [surgery] (cad=0) & (ischemia=1) -> 0.9 : (cad'=0) & (ischemia'=0) +
                                        0.1 : (cad'=0) & (ischemia'=1);
    [surgery] (cad=1) & (ischemia=1) -> 0.81 : (cad'=0) & (ischemia'=0) +
                                        0.09 : (cad'=0) & (ischemia'=1) +
                                        0.09 : (cad'=1) & (ischemia'=0) +
                                        0.01 : (cad'=1) & (ischemia'=1);

    [finish] (ecgResult=1) & (ecgResult=1) -> (done'=true);

endmodule



module patient
    chestPain : [0..1] init 0; // 0: absent, 1: present
    mi : [0..1] init 0; // Myocardial infarction ; 0: absent, 1: present
    
    [start] true -> 0.25 : (cad'=0) & (ischemia'=0) & (chestPain'=0) +
    		    0.125 : (cad'=0) & (ischemia'=1) & (chestPain'=0) +
                    0.125 : (cad'=0) & (ischemia'=1) & (chestPain'=1) +
                    0.125 : (cad'=1) & (ischemia'=0) & (chestPain'=0) +
                    0.125 : (cad'=1) & (ischemia'=0) & (chestPain'=1) +
                    0.20 : (cad'=1) & (ischemia'=1) & (chestPain'=1) +
                    0.05 : (cad'=1) & (ischemia'=1) & (chestPain'=0);
    [MI] (cad=0) & (ischemia=0) & (mi=0) -> 1 : (mi'=0);
    [MI] (cad=1) & (ischemia=1) & (mi=0) -> 0.81 : (mi'=0) + 0.19 : (mi'=1);
    [MI] (cad=1) & (ischemia=0) & (mi=0) -> 0.9 : (mi'=0) + 0.1 : (mi'=1);
    [MI] (cad=0) & (ischemia=1) & (mi=0) -> 0.9 : (mi'=0) + 0.1 : (mi'=1);
    [MI] (mi=1) -> 1 : (mi'=1) & (done'=true);
endmodule


label "done" = done;
label "bad" = mi=1;
label "notbad" =  !bad;
label "good" = good;

rewards "cost"
    [ecg] true : 20;
    [ST] true : 10;
    [medicine] true : 40;
    [surgery] true : 500;
    [MI] (mi=1) : 1000;
endrewards
